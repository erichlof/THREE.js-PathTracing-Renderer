<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js PathTracing Renderer - Cornell Box</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, initial-scale=1">
		<style>
			
			body {
				color: #ffffff;
				font-family:Monospace;
				font-size:13px;
				text-align:center;
				font-weight: bold;

				background-color: #000000;
				margin: 0px;
				overflow: hidden;
			}
			
			#instructions {
			
				font-family: arial;
				font-weight: bold;
				width: 100%;
				height: 100%;
				position: fixed;
				top: 35%;
				color: #ffffff;
				text-align: center;
				cursor: pointer;
			
			}

			#info {
				position: absolute;
				top: 0px; width: 100%;
				padding: 5px;
			}

			a {

				color: #ffffff;
			}

			#oldie a { color:#da0 }
		</style>
	</head>
	<body>

		<div id="container"></div>
		<div id="info"> three.js PathTracing Renderer - Cornell Box</div>
		
		<div id="instructions">
		<span style="font-size:40px"></span>
		</div>
		
		
		<div id="debug" style="position:fixed; left:3%; bottom:1%; font-family:arial; font-type:bold; color:rgb(255,255,255);">
		samples: 0
		</div>

		<script src="js/three-r81.min.js"> </script>
		<script src="js/threex.keyboardstate.js"> </script>
		<script src="js/FirstPersonCameraControls.js"> </script>
		<script src="js/virtualButtonJoystick.js"> </script>

		<script src="js/Detector.js"> </script>
		<script src="js/stats.min.js"> </script>
		
		
		<script id="screenTextureVertexShader" type="x-shader/x-vertex">

precision highp float;
precision highp int;

varying vec2 vUv;

void main() {

	vUv = uv;
	gl_Position = vec4( position, 1.0 );

}

		</script>
		
		<script id="screenTextureFragmentShader" type="x-shader/x-fragment">

precision highp float;
precision highp int;
precision highp sampler2D;

varying vec2 vUv;
uniform sampler2D tTexture0;


void main() {
	
	gl_FragColor = vec4( texture2D(tTexture0, vUv).rgb, 1.0 );
	
}
		
		</script>
		
		<script id="screenOutputVertexShader" type="x-shader/x-vertex">

precision highp float;
precision highp int;

varying vec2 vUv;

void main() {

	vUv = uv;
	gl_Position = vec4( position, 1.0 );

}

		</script>
		
		<script id="screenOutputFragmentShader" type="x-shader/x-fragment">

precision highp float;
precision highp int;
precision highp sampler2D;

varying vec2 vUv;
uniform float uOneOverSampleCounter;
uniform sampler2D tTexture0;

void main() {

	vec3 pixelColor = texture2D(tTexture0, vUv).rgb * uOneOverSampleCounter;
	
	//pixelColor = clamp(pixelColor, 0.0, 1.0);
	
	// apply gamma correction
	pixelColor = pow( pixelColor, vec3(0.4545) );
	
	gl_FragColor = vec4( pixelColor, 1.0 );
	
}
		
		</script>
		


		<script id="pathTracingVertexShader" type="x-shader/x-vertex">
		
precision highp float;
precision highp int;

varying vec2 vUv;

void main()
{
	vUv = uv;
	gl_Position = vec4( position, 1.0 );
}

		</script>
		
		
		
		<script id="pathTracingFragmentShader" type="x-shader/x-fragment">
				
precision highp float;
precision highp int;
precision highp sampler2D;

uniform bool uCameraIsMoving;
uniform bool uCameraHasJustStopped;

uniform float uTime;
uniform float uSampleCounter;
uniform float uULen;
uniform float uVLen;

uniform vec2 uResolution;

uniform vec3 uMeshBBox_min;
uniform vec3 uMeshBBox_max;
uniform vec3 uRandomVector;

uniform mat4 uCameraMatrix;
uniform mat4 uTallBoxMeshMatrix;
uniform mat4 uTallBoxMeshInvMatrix;
uniform mat4 uShortBoxMeshMatrix;
uniform mat4 uShortBoxMeshInvMatrix;

uniform sampler2D tPreviousTexture;
uniform sampler2D tTriangleTexture;

varying vec2 vUv;

#define PI               3.14159265358979323
#define ONE_OVER_PI      0.31830988618379067
#define TWO_PI           6.28318530717958648
#define FOUR_PI          12.5663706143591729
#define ONE_OVER_FOUR_PI 0.07957747154594767
#define PI_OVER_TWO      1.57079632679489662
#define E                2.71828182845904524


#define SPHERE_ID 0
#define PLANE_ID 1
#define DISK_ID 2
#define TRIANGLE_ID 3
#define QUAD_ID 4
#define BOX_ID 5
#define ELLIPSOID_ID 6
#define CYLINDER_ID 7
#define CONE_ID 8
#define OBJ_TRIANGLE_ID 9

#define N_QUADS 5
#define N_BOXES 3

#define DIFF 0
#define REFR 1
#define SPEC 2
#define CHECK 3
#define COAT 4
#define VOLUME 5
#define TRANSLUCENT 6
#define SPECSUB 7

vec3 debugSpherePos;

float seed = 0.0;

float rand()
{ 
	return fract( sin(seed++) * 43758.5453123 );
}


//-----------------------------------------------------------------------

struct Ray { vec3 origin; vec3 direction; };
struct Sphere { float radius; vec3 position; vec3 emission; vec3 color; int type; };
struct Quad { vec3 normal; vec3 v0; vec3 v1; vec3 v2; vec3 v3; vec3 emission; vec3 color; int type; };
struct Box { vec3 minCorner; vec3 maxCorner; vec3 emission; vec3 color; int type; };
struct Intersection { vec3 normal; vec3 emission; vec3 color; int type; int id; };

Quad quads[N_QUADS];
Box boxes[N_BOXES];


//----------------------------------------------------------------------------
float QuadIntersect( vec3 v0, vec3 v1, vec3 v2, vec3 v3, vec3 normal, Ray r )
//----------------------------------------------------------------------------
{
	vec3 u, v, n;    // triangle vectors
	vec3 w0, w, x;   // ray and intersection vectors
	float rt, a, b;  // params to calc ray-plane intersect
	
	// get first triangle edge vectors and plane normal
	v = v2 - v0;
	u = v1 - v0; // switched u and v names to save calculation later below
	//n = cross(v, u); // switched u and v names to save calculation later below
	n = -normal; // can avoid cross product if normal is already known
	    
	w0 = r.origin - v0;
	a = -dot(n,w0);
	b = dot(n, r.direction);
	if (b < 0.0001)   // ray is parallel to quad plane
		return -1.0;

	// get intersect point of ray with quad plane
	rt = a / b;
	if (rt < 0.0)          // ray goes away from quad
		return -1.0;   // => no intersect
	    
	x = r.origin + rt * r.direction; // intersect point of ray and plane

	// is x inside first Triangle?
	float uu, uv, vv, wu, wv, D;
	uu = dot(u,u);
	uv = dot(u,v);
	vv = dot(v,v);
	w = x - v0;
	wu = dot(w,u);
	wv = dot(w,v);
	D = 1.0 / (uv * uv - uu * vv);

	// get and test parametric coords
	float s, t;
	s = (uv * wv - vv * wu) * D;
	if (s >= 0.0 && s <= 1.0)
	{
		t = (uv * wu - uu * wv) * D;
		if (t >= 0.0 && (s + t) <= 1.0)
		{
			return rt;
		}
	}
	
	// is x inside second Triangle?
	u = v3 - v0;
	///v = v2 - v0;  //optimization - already calculated above

	uu = dot(u,u);
	uv = dot(u,v);
	///vv = dot(v,v);//optimization - already calculated above
	///w = x - v0;   //optimization - already calculated above
	wu = dot(w,u);
	///wv = dot(w,v);//optimization - already calculated above
	D = 1.0 / (uv * uv - uu * vv);

	// get and test parametric coords
	s = (uv * wv - vv * wu) * D;
	if (s >= 0.0 && s <= 1.0)
	{
		t = (uv * wu - uu * wv) * D;
		if (t >= 0.0 && (s + t) <= 1.0)
		{
			return rt;
		}
	}


	return -1.0;
}

//----------------------------------------------------------------------------
float BoxIntersect( vec3 minCorner, vec3 maxCorner, Ray r )
//----------------------------------------------------------------------------
{
	float epsilon = 0.001; // required to prevent self intersection

	vec3 invDir = 1.0 / r.direction;
	vec3 tmin = (minCorner - r.origin) * invDir;
	vec3 tmax = (maxCorner - r.origin) * invDir;

	vec3 real_min = min(tmin, tmax);
   	vec3 real_max = max(tmin, tmax);
   
   	float minmax = min( min(real_max.x, real_max.y), real_max.z);
   	float maxmin = max( max(real_min.x, real_min.y), real_min.z);

	if (minmax >= maxmin)
	{
		return maxmin > 0.0 ? maxmin : minmax;
	}
	
	return -1.0;
}

//-----------------------------------------------------------------------
bool SceneIntersect( Ray r, inout float t, inout Intersection intersec )
//-----------------------------------------------------------------------
{
	float d;
	float inf = 100000.0;
	t   = inf;
		
	for (int i = 0; i < N_QUADS; i++)
        {
		d = QuadIntersect( quads[i].v0, quads[i].v1, quads[i].v2, quads[i].v3, quads[i].normal, r );
		if (d > 0.0 && d < t)
		{
			t = d;
			intersec.normal = (quads[i].normal);
			intersec.emission = quads[i].emission;
			intersec.color = quads[i].color;
			intersec.type = quads[i].type;
			intersec.id = QUAD_ID;
		}
        }
	
	Ray rObj;
	
	for (int i = 0; i < N_BOXES; i++)
        {
		rObj = Ray(r.origin, r.direction);
		if(i == 1)
		{
			// transform ray into Tall Box's object space
			rObj.origin = vec3( uTallBoxMeshInvMatrix * vec4(rObj.origin, 1.0) );
			rObj.direction = vec3( uTallBoxMeshInvMatrix * vec4(rObj.direction, 0.0) );
		}
		if(i == 2)
		{
			// transform ray into Short Box's object space
			rObj.origin = vec3( uShortBoxMeshInvMatrix * vec4(rObj.origin, 1.0) );
			rObj.direction = vec3( uShortBoxMeshInvMatrix * vec4(rObj.direction, 0.0) );
		}
		
		d = BoxIntersect( boxes[i].minCorner, boxes[i].maxCorner, rObj );
		if (d > 0.0 && d < t)
		{
			
			t = d;
			vec3 point = rObj.origin + rObj.direction * t;
			vec3 minCorner = boxes[i].minCorner;
			vec3 maxCorner = boxes[i].maxCorner;
			vec3 normal;
			float epsilon = 0.001;

			if (abs(minCorner.x - point.x) < epsilon) normal = vec3(-1, 0, 0);
			else if (abs(maxCorner.x - point.x) < epsilon) normal = vec3(1, 0, 0);
			else if (abs(minCorner.y - point.y) < epsilon) normal = vec3(0, -1, 0);
			else if (abs(maxCorner.y - point.y) < epsilon) normal = vec3(0, 1, 0);
			else if (abs(minCorner.z - point.z) < epsilon) normal = vec3(0, 0, -1);
			else normal = vec3(0, 0, 1);

			if(i == 1)
			{
				// transform intersection point and normal back into world space
				point = vec3( uTallBoxMeshMatrix * vec4(point, 1.0) );
				t = distance(r.origin, point);
				normal = vec3( uTallBoxMeshMatrix * vec4(normal, 0.0) );
			}
			if(i == 2)
			{
				// transform intersection point and normal back into world space
				point = vec3( uShortBoxMeshMatrix * vec4(point, 1.0) );
				t = distance(r.origin, point);
				normal = vec3( uShortBoxMeshMatrix * vec4(normal, 0.0) );
			}
			
			intersec.normal = normal;
			intersec.emission = boxes[i].emission;
			intersec.color = boxes[i].color;
			intersec.type = boxes[i].type;
			intersec.id = BOX_ID;
		}
        }
	
	return t < inf;
}

vec3 randomDirectionInSphere()
{
    float up = rand() * 2.0 - 1.0; // cos(theta)
    float over = sqrt(1.0 - up * up); // sin(theta)
    float around = rand() * TWO_PI;

    return vec3( up, cos(around) * over, sin(around) * over );
}

vec3 randomCosWeightedDirectionInHemisphere(vec3 nl)
{
	float up = sqrt(rand()); // weighted cos(theta)
    	float over = sqrt(1.0 - up * up); // sin(theta)
    	float around = rand() * TWO_PI;
	vec3 u = normalize( cross( abs(nl.x) > 0.1 ? vec3(0, 1, 0) : vec3(1, 0, 0), nl ) );
	vec3 v = normalize( cross(nl, u) );
    	return vec3( cos(around) * over * u ) + ( sin(around) * over * v ) + (up * nl);		
}

vec3 computeTransmission(vec3 absorptionCoefficient, float distance) 
{
	return vec3( pow(vec3(E), ( -absorptionCoefficient * distance) ) );	
}


//-----------------------------------------------------------------------
vec3 CalculateRadiance( Ray r )
//-----------------------------------------------------------------------
{
	vec3 accumCol = vec3(0.0);
        vec3 mask = vec3(1.0);
        Intersection intersec;
	float inf = 100000.0;
	float t   = inf;
	
        for (int depth = 0; depth < 6; depth++)
	{
		
		if ( !SceneIntersect(r, t, intersec) )
		{
                    break;
		}
		
		// if we reached something bright, don't spawn any more rays
		if (intersec.emission.r > 0.1 || intersec.emission.b > 0.1 || intersec.emission.g > 0.1 )
		{
			
			accumCol += mask * intersec.emission;
			break;
		}
		
		vec3 n = normalize(intersec.normal);
                vec3 nl = dot(n,r.direction) <= 0.0 ? normalize(n) : normalize(n * -1.0);
		vec3 x = r.origin + r.direction * t;
		
                vec3 f = intersec.color;
		
		    /*
			// Russian Roulette
			// in original smallPT, only used if trace path nodes length is greater than 5)
			float p = max(max(f.x,f.y),f.z);
			if ( rand() < p)
			    f = f / p;
			else
			    break;
		    */ 
		    
                if (intersec.type == DIFF || intersec.type == CHECK) // Ideal DIFFUSE reflection
                {
			
			if( intersec.type == CHECK )
			{
				if( (mod(x.x,60.0) < 30.0 && mod(x.z,60.0) < 30.0) || 
					(mod(x.x,60.0) > 30.0 && mod(x.z,60.0) > 30.0) )
				f *= 0.3;					
			}
		
			// choose random Diffuse sample vector
			vec3 d = randomCosWeightedDirectionInHemisphere( nl );
                	r = Ray( x, normalize(d) );
			r.origin += r.direction;
			mask *= f;
                	continue;
                }
                if (intersec.type == SPEC)  // Ideal SPECULAR reflection
                {
			r = Ray( x, reflect(r.direction, nl) );
			r.origin += r.direction;
			mask *= f;
                        continue;
                }

                if (intersec.type == REFR)  // Ideal dielectric REFRACTION
		{
                
			bool into = dot(n,nl) > 0.0;  // Ray from outside going in?
			float nc = 1.0; // IOR of air
			float nt = 1.5; // IOR of solid glass
			if ( !into ) 
			{
				nc = 1.5;
				nt = 1.0;
			}
			
		/*
			// Original Fresnel equations
			float cosThetaInc = dot(nl, r.direction);
			float cosThetaTra = dot(nl, tdir);
			float coefS = (nc * cosThetaInc - nt * cosThetaTra) / (nc * cosThetaInc + nt * cosThetaTra);
			float coefP = (nc * cosThetaTra - nt * cosThetaInc) / (nc * cosThetaTra + nt * cosThetaInc);
			float Re = ( (coefS * coefS) + (coefP * coefP) ) * 0.5; // Unpolarized
			//float Tr = 1.0 - Re;
		*/			
			// Schlick Fresnel approx.
			float R0 = (nc - nt) / (nc + nt);
			R0 *= R0;
			float ddn = dot(-r.direction, nl);
			float c = 1.0 - ddn;
			float Re = R0 + (1.0 - R0) * c * c * c * c * c;

			if( rand() < Re )
			{
				r = Ray( x, reflect(r.direction, nl) );
			    	r.origin += r.direction;
			    	continue;
			}
			else
			{
			    	float nnt = nc / nt;
			    	vec3 tdir = refract(r.direction, nl, nnt);
			    	r = Ray(x, normalize(tdir));
			    	r.origin += r.direction;
			    	mask *= f;
			    	continue;
			}
		
		}
		
		if (intersec.type == COAT)  // Diffuse object underneath with ClearCoat on top (like car, or shiny pool ball)
		{
			
			// Schlick Fresnel approx.
			float ddn = dot(-r.direction, nl);
			float nc = 1.0; // IOR of air
			float nt = 1.4; // IOR of ClearCoat 
			float R0 = (nc - nt) / (nc + nt);
			R0 *= R0;
			float c = 1.0 - ddn;
			float Re = R0 + (1.0 - R0) * c * c * c * c * c;
			
			float shininess = 1.0;
			
			// choose random sample vector for diffuse material underneath ClearCoat
			vec3 d = randomCosWeightedDirectionInHemisphere( nl );
			
			// choose either specular reflection or diffuse
			if( rand() < Re )
			{	
				r = Ray( x, mix( normalize(d), reflect(r.direction, nl), shininess ) );
				r.origin += r.direction;
				continue;	
			}
			else
			{
				r = Ray( x, normalize(d) );
				r.origin += r.direction;
				mask *= f;
				continue;  	
			}
			
		} //end if (intersec.type == COAT)
		
		if (intersec.type == TRANSLUCENT)  // Translucent Sub-Surface Scattering material
                {
			float translucentDensity = 0.02;
			float scatteringDistance = -log( rand() ) / translucentDensity;
			if (scatteringDistance < t) {
				// Scatter the ray:
				x = x + r.direction * scatteringDistance;
				vec3 d = randomDirectionInSphere(); // Isotropic scattering
				r = Ray( x, d );
				r.origin += r.direction;
				// Compute how much light was absorbed along the ray before it was scattered:
				mask *= computeTransmission(vec3(0.9, 0.1, 0.01), scatteringDistance);
				continue;
			} else 
			{
				x = x + r.direction * scatteringDistance;
				///vec3 d = randomDirectionInSphere();
				vec3 d = randomCosWeightedDirectionInHemisphere( nl );
				r = Ray( x, d );
				///r = Ray( x, r.direction ); //transmission
				r.origin += r.direction;
				mask *= computeTransmission(vec3(0.9, 0.1, 0.01), t);
				continue;
			}
		
                } // end if (intersec.type == TRANSLUCENT)
		
		if (intersec.type == SPECSUB)  // Shiny(specular) coating over Sub-Surface Scattering material
                {
			// Schlick Fresnel approx.
			float ddn = dot(-r.direction, nl);
			float nc = 1.0; // IOR of air
			float nt = 1.1; // IOR of shiny coating 
			float R0 = (nc - nt) / (nc + nt);
			R0 *= R0;
			float c = 1.0 - ddn;
			float Re = R0 + (1.0 - R0) * c * c * c * c * c;
			
			if (rand() < Re)
			{
				r = Ray( x, reflect(r.direction, nl) );
				r.origin += r.direction;
				//mask *= f; // not needed, White specular reflection
                        	continue;
			}
			float translucentDensity = 0.1; //0.02
			float scatteringDistance = -log(rand()) / translucentDensity;
			if (scatteringDistance < t) 
			{
				// Scatter the ray:
				x = x + r.direction * scatteringDistance;
				vec3 d = randomDirectionInSphere(); // Isotropic scattering
				r = Ray( x, d );
				r.origin += r.direction;
				// Compute how much light was absorbed along the ray before it was scattered:
				mask *= computeTransmission(vec3(0.0, 100.0, 100.0), scatteringDistance);
				continue;
			} else 
			{
				x = x + r.direction * scatteringDistance;
				vec3 d = randomDirectionInSphere();
				///vec3 d = randomCosWeightedDirectionInHemisphere( nl );
				r = Ray( x, d );
				///r = Ray( x, r.direction ); //transmission
				r.origin += r.direction;
				mask *= computeTransmission(vec3(0.0, 100.0, 100.0), t);
				continue;
			}
		
                } // end if (intersec.type == SPECSUB)
		
		if (intersec.type == VOLUME)  // Volume filled with participating medium (i.e. smoke, fog, dust, etc.)
		{
                
			float density = 0.25;
			if (rand() < density)
			{
				// scattering
				// choose cosWeighted random Diffuse reflection vector for participating medium
				vec3 d = randomCosWeightedDirectionInHemisphere( nl ); //weighted scattering (reflects light better)
				//vec3 d = randomDirectionInSphere(); //true isotropic scattering
				r = Ray( x, normalize(d) );
				r.origin += r.direction;
				mask *= f;
				continue;
			}
			else
			{
				// transmission
				r = Ray( x, r.direction );
				r.origin += r.direction;
				continue;
			}
				
		} // end if (intersec.type == VOLUME)
	
	} // end for (int depth = 0; depth < 6; depth++)
	
	return accumCol;      
}

//-----------------------------------------------------------------------
void SetupScene(void)
//-----------------------------------------------------------------------
{
	vec3 z  = vec3(0.0, 0.0, 0.0);// No color value, Black        
	vec3 L1 = vec3(0.736507, 0.642866, 0.250431) * 30.0;// Bright Yellowish light
		                         //0.210431
        boxes[0]  = Box( vec3(213.0,548.0,-227.0), vec3(343.0,549.0,-332.0), L1, z, DIFF);// Area Light Box in ceiling
	
	boxes[1]  = Box( vec3(-82.0,-165.0, -80.0), vec3(82.0,165.0, 80.0), z, vec3(1.0, 1.0, 1.0 ), SPEC);// Tall Mirror Box Left
	boxes[2]  = Box( vec3(-86.0, -85.0, -80.0), vec3(86.0, 85.0, 80.0), z, vec3(0.99,0.99,0.99), DIFF);// Short Diffuse Box Right
	
	quads[0] = Quad( vec3( 0.0, 0.0, 1.0 ), vec3(   0.0,   0.0,-559.2), vec3( 549.6,   0.0,-559.2), vec3( 549.6, 548.8,-559.2), vec3(   0.0, 548.8,-559.2), z, vec3(0.99,0.99,0.99), DIFF);// Back Wall
	quads[1] = Quad( vec3( 1.0, 0.0, 0.0 ), vec3(   0.0,   0.0,   0.0), vec3(   0.0,   0.0,-559.2), vec3(   0.0, 548.8,-559.2), vec3(   0.0, 548.8,   0.0), z, vec3(0.7, 0.12,0.05), DIFF);// Left Wall Red
	quads[2] = Quad( vec3(-1.0, 0.0, 0.0 ), vec3( 549.6,   0.0,-559.2), vec3( 549.6,   0.0,   0.0), vec3( 549.6, 548.8,   0.0), vec3( 549.6, 548.8,-559.2), z, vec3(0.2, 0.4, 0.36), DIFF);// Right Wall Green
	quads[3] = Quad( vec3( 0.0,-1.0, 0.0 ), vec3(   0.0, 548.8,-559.2), vec3( 549.6, 548.8,-559.2), vec3( 549.6, 548.8,   0.0), vec3(   0.0, 548.8,   0.0), z, vec3(0.99,0.99,0.99), DIFF);// Ceiling
	quads[4] = Quad( vec3( 0.0, 1.0, 0.0 ), vec3(   0.0,   0.0,   0.0), vec3( 549.6,   0.0,   0.0), vec3( 549.6,   0.0,-559.2), vec3(   0.0,   0.0,-559.2), z, vec3(0.99,0.99,0.99), DIFF);// Floor
	
}



void main( void )
{

	vec3 camPos     = vec3( uCameraMatrix[3][0],  uCameraMatrix[3][1],  uCameraMatrix[3][2]);
	
    	vec3 camRight   = vec3( uCameraMatrix[0][0],  uCameraMatrix[0][1],  uCameraMatrix[0][2]);
    	vec3 camUp      = vec3( uCameraMatrix[1][0],  uCameraMatrix[1][1],  uCameraMatrix[1][2]);
	vec3 camForward = vec3(-uCameraMatrix[2][0], -uCameraMatrix[2][1], -uCameraMatrix[2][2]);
	
	// seed for rand() function
	seed = mod(uSampleCounter,100.0) * uRandomVector.x + uRandomVector.y + uResolution.y * gl_FragCoord.x / uResolution.x + uResolution.x * gl_FragCoord.y / uResolution.y;
	
	float r1 = 2.0 * rand();
	float r2 = 2.0 * rand();
	
	vec2 d;
	d.x = r1 < 1.0 ? sqrt(r1) - 1.0 : 1.0 - sqrt(2.0 - r1);
        d.y = r2 < 1.0 ? sqrt(r2) - 1.0 : 1.0 - sqrt(2.0 - r2);
	
	// TODO save this as a uniform to avoid division
	d *= (1.0 / uResolution);
	d += vUv - 0.5;
	
	vec3 rayDir = normalize( d.x * camRight * uULen + d.y * camUp * uVLen + camForward );
	
	// depth of field
	float aperture    = 0.025;
	float focalLength = 1000.0;
    	vec3 focalPoint = focalLength * rayDir;
	  
    	//pick random point on aperture
    	float randomAngle = rand() * TWO_PI;
    	float randomRadius = rand() * aperture;
    	vec3  randomAperturePos = ( cos(randomAngle) * camRight + sin(randomAngle) * camUp ) * randomRadius;
    
    	//point on aperture to focal point
    	vec3 finalRayDir = normalize(focalPoint - randomAperturePos);
    
	Ray ray = Ray( camPos + randomAperturePos , finalRayDir );

	// debug DOF(depth of field) - uncomment 2nd line below to make sphere match your focal point
	debugSpherePos = vec3(50.0,14.2,-90.0); // place sphere on ground
	//debugSpherePos = camPos + focalLength * camForward; // DOF debug mode, sphere follows your line of sight
	
	SetupScene();
	     		
	// perform path tracing and get resulting pixel color
	vec3 pixelColor = CalculateRadiance( ray );
	
	vec3 previousColor = texture2D(tPreviousTexture, vUv).rgb;
	
	if ( uCameraHasJustStopped )
	{
		previousColor = vec3(0); // clear rendering accumulation buffer
	}
	else if ( uCameraIsMoving )
	{
		previousColor *= 0.4; // motion-blur trail amount
		pixelColor *= (uSampleCounter * 0.7); // brightness
	}
	
	gl_FragColor = vec4( pixelColor + previousColor, 1.0 );
	
}

		</script>
		
		
		<script>

			if ( ! Detector.webgl ) Detector.addGetWebGLMessage();

			var SCREEN_WIDTH = window.innerWidth;
			var SCREEN_HEIGHT = window.innerHeight;
			var container, stats;
			var controls;
			var pathTracingScene, screenTextureScene, screenOutputScene;
			var pathTracingUniforms, screenTextureUniforms, screenOutputUniforms;
			var pathTracingDefines;
			var pathTracingGeometry, pathTracingMaterial, pathTracingMesh;
			var screenTextureGeometry, screenTextureMaterial, screenTextureMesh;
			var screenOutputGeometry, screenOutputMaterial, screenOutputMesh;
			var pathTracingRenderTarget, screenOutputRenderTarget;
			var quadCamera, worldCamera;
			var tallBoxGeometry, tallBoxMaterial, tallBoxMesh;
			var shortBoxGeometry, shortBoxMaterial, shortBoxMesh;
			var renderer, clock;
			var frameTime, elapsedTime;
			var fovScale;
			var pixelRatio = window.devicePixelRatio * 0.6;
			var decreaseResolution = false;
			var TWO_PI = Math.PI * 2;
			var randomVector = new THREE.Vector3();
			var sampleCounter = 0.0;
			var keyboard = new THREEx.KeyboardState();
			var cameraIsMoving = false;
			var cameraHasJustStopped = false;
			var cameraRecentlyMoving = false;
			var clearRenderBufferTimer = 0.0;
			var isPaused = true;
			var oldYawRotation, oldPitchRotation;
			var camFlightSpeed = 300;
			var fontAspect;
			
			// the following variables will be used to calculate rotations and directions from the camera
			var cameraDirectionVector = new THREE.Vector3();//for moving where the camera is looking
			var cameraRightVector = new THREE.Vector3();//for strafing the camera right and left
			var cameraUpVector = new THREE.Vector3();//for moving camera up and down
			var cameraWorldQuaternion = new THREE.Quaternion();//for rotating scene objects to match camera's current rotation
			var cameraControlsObject;//for positioning and moving the camera itself
			var cameraControlsYawObject;//allows access to control camera's left/right movements through mobile input
			var cameraControlsPitchObject;//allows access to control camera's up/down movements through mobile input


			// are we in portrait mobile view? if so, move the buttons over to the left a little..
			// if not and we are in landscape mode, they can safely be moved farther right without running into each other
			var b2PercentLeft = SCREEN_WIDTH < SCREEN_HEIGHT ? 50 : 65;
			var b1PercentLeft = SCREEN_WIDTH < SCREEN_HEIGHT ? 77 : 81;
			var b3PercentLeft = Math.floor( (b1PercentLeft + b2PercentLeft) / 2 );
			var joystick = new VirtualJoystick({
				add3Buttons: true,
				hideJoystick: true,
				hideButtons: false,
				button1PercentLeft: b1PercentLeft,
				button2PercentLeft: b2PercentLeft,
				button3PercentLeft: b3PercentLeft
			});

			var PI_2 = Math.PI / 2;//used by controls below
			
			var infoElement = document.getElementById( 'info' );
			infoElement.style.cursor = "default";
			infoElement.style.webkitUserSelect = "none";
			infoElement.style.MozUserSelect = "none";
			
			var instructionsElement = document.getElementById( 'instructions' );
			instructionsElement.style.cursor = "default";
			instructionsElement.style.webkitUserSelect = "none";
			instructionsElement.style.MozUserSelect = "none";
			
			var debugElement = document.getElementById( 'debug' );
			debugElement.style.cursor = "default";
			debugElement.style.webkitUserSelect = "none";
			debugElement.style.MozUserSelect = "none";
			
			var mouseControl = true;

			if ( 'createTouch' in document ) {
				mouseControl = false;
				pixelRatio = window.devicePixelRatio * 0.15;
			}
			
			// if on mobile device, unpause the app because there is no ESC key and no mouse capture to do
			if ( !mouseControl )
				isPaused = false;
			
			if (mouseControl) {
	
				instructionsElement.innerHTML = 'Paused <br> Click to start';

				document.body.addEventListener("click", function() {
					this.requestPointerLock = this.requestPointerLock || this.mozRequestPointerLock;
					this.requestPointerLock();
				}, false);

				window.addEventListener("click", function(event) {
					event.preventDefault();	
				}, false);
				window.addEventListener("dblclick", function(event) {
					event.preventDefault();	
				}, false);


				var pointerlockChange = function ( event ) {

					if ( document.pointerLockElement === instructionsElement || document.mozPointerLockElement === instructionsElement || document.webkitPointerLockElement === instructionsElement ||
						document.pointerLockElement === document.body || document.mozPointerLockElement === document.body || document.webkitPointerLockElement === document.body ) {

						instructionsElement.style.display = 'none';
						isPaused = false;

					} else {

						instructionsElement.style.display = '';
						isPaused = true;

					}

				};

				// Hook pointer lock state change events
				document.addEventListener( 'pointerlockchange', pointerlockChange, false );
				document.addEventListener( 'mozpointerlockchange', pointerlockChange, false );
				document.addEventListener( 'webkitpointerlockchange', pointerlockChange, false );

			}
			
				
			init();
					
  							
			function init() {

				container = document.getElementById( 'container' );
				renderer = new THREE.WebGLRenderer();
				renderer.autoClear = false;
				renderer.setPixelRatio(pixelRatio);
				
				container.appendChild( renderer.domElement );

				renderer.context.getExtension('OES_texture_float');
							      
				stats = new Stats();
				stats.domElement.style.position = 'absolute';
				stats.domElement.style.top = '0px';
				container.appendChild( stats.domElement );
				
				window.addEventListener( 'resize', onWindowResize, false );
				
				clock = new THREE.Clock();
				
				pathTracingScene = new THREE.Scene();
				screenTextureScene = new THREE.Scene();
				screenOutputScene = new THREE.Scene();
				
				// quadCamera is simply the camera to help render the full screen quad (2 triangles),
				// hence the name.  It is an Orthographic camera that sits facing the view plane, which serves as
				// the window into our 3d world. This camera will not move or rotate for the duration of the app.
				quadCamera = new THREE.OrthographicCamera( -1, 1, 1, -1, 0, 1 );
				screenTextureScene.add(quadCamera);
				screenOutputScene.add(quadCamera);
				
				// worldCamera is the dynamic camera 3d object that will be positioned, oriented and 
				// constantly updated inside the 3d scene.  Its view will ultimately get passed back to the 
				// stationary quadCamera, which renders the scene to a fullscreen quad (made up of 2 large triangles).
				worldCamera = new THREE.PerspectiveCamera(60, window.innerWidth / window.innerHeight, 1, 1000);
				pathTracingScene.add(worldCamera);
				
				controls = new FirstPersonCameraControls( worldCamera );
							
				cameraControlsObject = controls.getObject();
				cameraControlsYawObject = controls.getYawObject();
				cameraControlsPitchObject = controls.getPitchObject();
				
				pathTracingScene.add( cameraControlsObject );

				// for flyCam
				cameraControlsObject.position.set(278, 273, 1000);
				///cameraControlsYawObject.rotation.y = 0.0;
				// look slightly downward
				cameraControlsPitchObject.rotation.x = 0.0;
				joystick.previousRotationX = 0.0;
				
				// now that we moved and rotated the camera, the following line force updates the camera's matrix,
				//  and prevents rendering the very first frame in the old default camera position/orientation
				cameraControlsObject.updateMatrixWorld(true);
				
				pathTracingRenderTarget = new THREE.WebGLRenderTarget( (window.innerWidth * pixelRatio), (window.innerHeight * pixelRatio), {
					minFilter: THREE.NearestFilter,
					magFilter: THREE.NearestFilter,
					format: THREE.RGBAFormat,
					type: THREE.FloatType,
					depthBuffer: false,
					stencilBuffer: false
				} );
				pathTracingRenderTarget.texture.generateMipmaps = false;
				
				screenTextureRenderTarget = new THREE.WebGLRenderTarget( (window.innerWidth * pixelRatio), (window.innerHeight * pixelRatio), {
					minFilter: THREE.NearestFilter, 
					magFilter: THREE.NearestFilter,
					format: THREE.RGBAFormat,
					type: THREE.FloatType,
					depthBuffer: false,
					stencilBuffer: false
				} );
				screenTextureRenderTarget.texture.generateMipmaps = false;
				
				
				pathTracingGeometry = new THREE.PlaneBufferGeometry( 2, 2 );

				pathTracingUniforms = {
					
					tPreviousTexture: { type: "t", value: screenTextureRenderTarget.texture },
					
					uCameraIsMoving: { type: "b1", value: false },
					uCameraHasJustStopped: { type: "b1", value: false },
					
					uTime: { type: "f", value: 0.0 },
					uSampleCounter: { type: "f", value: 0.0 },
					uULen: { type: "f", value: 1.0 },
					uVLen: { type: "f", value: 1.0 },
					
					uResolution: { type: "v2", value: new THREE.Vector2() },
					
					uRandomVector: { type: "v3", value: new THREE.Vector3() },
				
					uCameraMatrix: { type: "m4", value: new THREE.Matrix4() },
					uTallBoxMeshMatrix: { type: "m4", value: new THREE.Matrix4() },
					uTallBoxMeshInvMatrix: { type: "m4", value: new THREE.Matrix4() },
					uShortBoxMeshMatrix: { type: "m4", value: new THREE.Matrix4() },
					uShortBoxMeshInvMatrix: { type: "m4", value: new THREE.Matrix4() }
				};
				
				
				pathTracingMaterial = new THREE.ShaderMaterial( {
					uniforms: pathTracingUniforms,
					vertexShader: document.getElementById( 'pathTracingVertexShader' ).textContent,
					fragmentShader: document.getElementById( 'pathTracingFragmentShader' ).textContent,
				        depthTest: false,
                                        depthWrite: false
                                } );

				pathTracingMesh = new THREE.Mesh( pathTracingGeometry, pathTracingMaterial );
				pathTracingScene.add( pathTracingMesh );
				
				
				
				// the following keeps the large scene ShaderMaterial quad right in front 
				//   of the camera at all times. This is necessary because without it, the scene 
				//   quad will fall out of view and get clipped when the camera rotates past 180 degrees.
				worldCamera.add( pathTracingMesh );
				
				
				
				screenTextureGeometry = new THREE.PlaneBufferGeometry( 2, 2 );
				
				screenTextureUniforms = {
					tTexture0: { type: "t", value: pathTracingRenderTarget.texture }
				}
				
				screenTextureMaterial = new THREE.ShaderMaterial( {
					uniforms: screenTextureUniforms,
					vertexShader: document.getElementById( 'screenTextureVertexShader' ).textContent,
					fragmentShader: document.getElementById( 'screenTextureFragmentShader' ).textContent,
					depthWrite: false,
					depthTest: false
				} );
				
				screenTextureMesh = new THREE.Mesh(screenTextureGeometry, screenTextureMaterial);
				screenTextureScene.add(screenTextureMesh);
				
				
				
			
				screenOutputGeometry = new THREE.PlaneBufferGeometry( 2, 2 );
				
				screenOutputUniforms = {
					uOneOverSampleCounter: { type: "f", value: 0.0 },
					tTexture0: { type: "t", value: pathTracingRenderTarget.texture }
				}
				
				screenOutputMaterial = new THREE.ShaderMaterial( {
					uniforms: screenOutputUniforms,
					vertexShader: document.getElementById( 'screenOutputVertexShader' ).textContent,
					fragmentShader: document.getElementById( 'screenOutputFragmentShader' ).textContent,
					depthWrite: false,
					depthTest: false
				} );
				
				screenOutputMesh = new THREE.Mesh(screenOutputGeometry, screenOutputMaterial);
				screenOutputScene.add(screenOutputMesh);

				// Boxes
				tallBoxGeometry = new THREE.BoxGeometry(1,1,1);
				tallBoxMaterial = new THREE.MeshPhysicalMaterial( {
					color: new THREE.Color(0.95, 0.95, 0.95), //RGB, ranging from 0.0 - 1.0
					roughness: 1.0 // ideal Diffuse material	
				} );
				
				tallBoxMesh = new THREE.Mesh(tallBoxGeometry, tallBoxMaterial);
				pathTracingScene.add(tallBoxMesh);
				tallBoxMesh.visible = false; // disable normal Three.js rendering updates of this object: 
				// it is just a data placeholder as well as an Object3D that can be transformed/manipulated by 
				// using familiar Three.js library commands. It is then fed into the GPU path tracing renderer
				// through its 'matrixWorld' matrix. See below:
				tallBoxMesh.rotation.set(0, Math.PI * 0.105, 0);
				tallBoxMesh.position.set(180, 165, -350);
				tallBoxMesh.updateMatrixWorld(true); // 'true' forces immediate matrix update
				pathTracingUniforms.uTallBoxMeshMatrix.value.copy( tallBoxMesh.matrixWorld );
				pathTracingUniforms.uTallBoxMeshInvMatrix.value.getInverse( tallBoxMesh.matrixWorld );
				
				shortBoxGeometry = new THREE.BoxGeometry(1,1,1);
				shortBoxMaterial = new THREE.MeshPhysicalMaterial( {
					color: new THREE.Color(0.95, 0.95, 0.95), //RGB, ranging from 0.0 - 1.0
					roughness: 1.0 // ideal Diffuse material	
				} );
				
				shortBoxMesh = new THREE.Mesh(shortBoxGeometry, shortBoxMaterial);
				pathTracingScene.add(shortBoxMesh);
				shortBoxMesh.visible = false;
				shortBoxMesh.rotation.set(0, -Math.PI * 0.09, 0);
				shortBoxMesh.position.set(370, 85, -170);
				shortBoxMesh.updateMatrixWorld(true); // 'true' forces immediate matrix update
				pathTracingUniforms.uShortBoxMeshMatrix.value.copy( shortBoxMesh.matrixWorld );
				pathTracingUniforms.uShortBoxMeshInvMatrix.value.getInverse( shortBoxMesh.matrixWorld );
				
				// onWindowResize() must be at the end of the init() function
				onWindowResize();
				
				// everything is set up, now we can start animating
				animate();
				
			} // end function init()
			
			

			function onWindowResize( event ) {
				
				SCREEN_WIDTH = window.innerWidth;
				SCREEN_HEIGHT = window.innerHeight;
				renderer.setSize( SCREEN_WIDTH, SCREEN_HEIGHT );
				
				fontAspect = (SCREEN_WIDTH / 175) * (SCREEN_HEIGHT / 200);
				if (fontAspect > 25) fontAspect = 25;
				if (fontAspect < 4) fontAspect = 4;
				fontAspect *= 2;
				instructionsElement.style.fontSize = fontAspect + "px";
	

				pathTracingUniforms.uResolution.value.x = SCREEN_WIDTH * pixelRatio;
				pathTracingUniforms.uResolution.value.y = SCREEN_HEIGHT * pixelRatio;
				
				pathTracingRenderTarget.setSize( SCREEN_WIDTH * pixelRatio, SCREEN_HEIGHT * pixelRatio );
				screenTextureRenderTarget.setSize( SCREEN_WIDTH * pixelRatio, SCREEN_HEIGHT * pixelRatio );
				
				worldCamera.aspect = SCREEN_WIDTH / SCREEN_HEIGHT;
				worldCamera.updateProjectionMatrix();
				
				// the following scales all scene objects by the worldCamera's field of view,
				// taking into account the screen aspect ratio and multiplying the uniform uULen,
				// the x-coordinate, by this ratio
				fovScale = worldCamera.fov * 0.5 * (Math.PI / 180.0);
				pathTracingUniforms.uVLen.value = Math.tan(fovScale);
				pathTracingUniforms.uULen.value = pathTracingUniforms.uVLen.value * worldCamera.aspect;
				
				// check if mobile device is in portrait or landscape mode and position buttons accordingly
				b2PercentLeft = SCREEN_WIDTH < SCREEN_HEIGHT ? 50 : 65;
				joystick._button2El.style.left = b2PercentLeft + "%";
				b1PercentLeft = SCREEN_WIDTH < SCREEN_HEIGHT ? 77 : 81;
				joystick._button1El.style.left = b1PercentLeft + "%";
				joystick._button3El.style.left = Math.floor( (b1PercentLeft + b2PercentLeft) / 2 ) + "%";
				if (SCREEN_WIDTH < SCREEN_HEIGHT ) {
					joystick._button3El.style.bottom = 11 + "%";
				}
				else if (SCREEN_WIDTH > SCREEN_HEIGHT ) {
					joystick._button3El.style.bottom = 21 + "%";
				}
				
			} // end function onWindowResize( event )
			


			function animate() {
				
				requestAnimationFrame( animate );
				
				frameTime = clock.getDelta();
				
				elapsedTime = clock.getElapsedTime() % 1000;
				
				// reset flags
				cameraIsMoving = false;
				cameraHasJustStopped = false;
				
				// check user controls
				if (mouseControl) {
					// movement detected
					if ( oldYawRotation != cameraControlsYawObject.rotation.y || 
					      oldPitchRotation != cameraControlsPitchObject.rotation.x ) {
	
						cameraIsMoving = true;
					}
					
					// save state for next frame
					oldYawRotation = cameraControlsYawObject.rotation.y;
					oldPitchRotation = cameraControlsPitchObject.rotation.x;
					
				} // end if (mouseControl)
			
				// if not playing on desktop, get the rotation from the mobile-touch virtual Joystick
				if ( !mouseControl ) {

					cameraControlsYawObject.rotation.y = joystick.previousRotationY - joystick.deltaX() * 0.005;
					cameraControlsPitchObject.rotation.x = joystick.previousRotationX - joystick.deltaY() * 0.005;
					// clamp the camera's vertical movement (around the x-axis) to the 'ceiling' and 'floor',
					// so you can't accidentally flip the camera upside down
					cameraControlsPitchObject.rotation.x = Math.max( - PI_2, Math.min( PI_2, cameraControlsPitchObject.rotation.x ) );
					
					// movement detected
					if ( joystick.deltaX() || joystick.deltaY() ) {
							
						cameraIsMoving = true;
					}
					
				} // end if ( !mouseControl )
				
				// this gives us a vector in the direction that the camera is pointing,
				// which will be useful for moving the camera 'forward' and shooting projectiles in that direction
				controls.getDirection(cameraDirectionVector);
				//cameraDirectionVector.normalize();
				controls.getUpVector(cameraUpVector);
				controls.getRightVector(cameraRightVector);

				// the following gives us a rotation quaternion (4D vector), which will be useful for 
				// rotating scene objects to match the camera's rotation
				worldCamera.getWorldQuaternion(cameraWorldQuaternion);
				
				// allow flying camera
				if ( joystick.button3Pressed || keyboard.pressed('W') ) {

					cameraControlsObject.position.add(cameraDirectionVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				if ( keyboard.pressed('S') ) {

					cameraControlsObject.position.sub(cameraDirectionVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				if ( joystick.button1Pressed || keyboard.pressed('D') ) {

					cameraControlsObject.position.add(cameraRightVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				if ( keyboard.pressed('A') ) {

					cameraControlsObject.position.sub(cameraRightVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				if ( joystick.button2Pressed || keyboard.pressed('Q') ) {

					cameraControlsObject.position.add(cameraUpVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				if ( keyboard.pressed('Z') ) {

					cameraControlsObject.position.sub(cameraUpVector.multiplyScalar(camFlightSpeed * frameTime));
					cameraIsMoving = true;
				}
				
				// movement detected
				if ( cameraIsMoving ) {
					clearRenderBufferTimer = 0.0;
					cameraRecentlyMoving = true;
				}
				// no movement detected
				if ( !cameraIsMoving ) {
					
					if ( cameraRecentlyMoving ) {
						clearRenderBufferTimer += frameTime;
					}
					
					if ( clearRenderBufferTimer > 0.0 ) {
						sampleCounter = 1.0;
						cameraHasJustStopped = true;
						clearRenderBufferTimer = 0.0;
						cameraRecentlyMoving = false;
					}
					
				}
				
				pathTracingUniforms.uCameraIsMoving.value = cameraIsMoving;
				pathTracingUniforms.uCameraHasJustStopped.value = cameraHasJustStopped;
				pathTracingUniforms.uSampleCounter.value = sampleCounter;
				screenOutputUniforms.uOneOverSampleCounter.value = 1.0 / sampleCounter;
				pathTracingUniforms.uRandomVector.value = randomVector.set( Math.random(), Math.random(), Math.random() );
				// CAMERA
				cameraControlsObject.updateMatrixWorld(true);			
				pathTracingUniforms.uCameraMatrix.value.copy( worldCamera.matrixWorld );
				
				debugElement.innerHTML = "samples: " + sampleCounter;
				if ( !cameraIsMoving ) {
					sampleCounter += 1.0;
				}
				
				// RENDERING in 3 steps
				
				// STEP 1
				// Perform PathTracing and Render(save) into pathTracingRenderTarget
				// Read previous screenTextureRenderTarget to use as a new starting point to blend with
				renderer.render( pathTracingScene, worldCamera, pathTracingRenderTarget );	
				
				// STEP 2
				// Render(copy) the final pathTracingScene output(above) into screenTextureRenderTarget
				// This will be used as a new starting point for Step 1 above
				renderer.render( screenTextureScene, quadCamera, screenTextureRenderTarget );
				
				// STEP 3
				// Render full screen quad with generated pathTracingRenderTarget above.
				// After the image is gamma corrected, it will be shown on the screen as the final accumulated output
				renderer.render( screenOutputScene, quadCamera );
				
				
				
				stats.update();
				
					
			} // end function animate()

		</script>

	</body>
</html>
